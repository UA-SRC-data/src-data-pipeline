prefix dwc: <http://rs.tdwg.org/dwc/terms/>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix dc: <http://purl.org/dc/elements/1.1/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ppo: <http://www.plantphenology.org/id/>
prefix urn: <urn:>

# Use the concat/group_concat function to push all plantStructurePresence types into an array within a field.
# this enables ElasticSearch to index this easily while shrinking output file sizes
SELECT ?observationID ?directory ?source ?location_name ?location_type ?collected_on ?medium ?unit ?variable_name ?variable_desc ?value 
	(group_concat(distinct ?traitTypes;separator='|') as ?traits)

WHERE {    
	# Create a list of environmental Traits
    	?environmentalTrait rdf:type ?traitTypes .
    	#?environmentalTrait rdfs:label ?traitLabels .

	# Do not report namedindividual, not particularly useful in our output
    	FILTER (?traitTypes != <http://www.w3.org/2002/07/owl#NamedIndividual>) .
	# Do not the higher level classes, which are not particularly useful
    	FILTER (?traitTypes != obo:BFO_0000002) .
    	FILTER (?traitTypes != obo:BFO_0000020) .
    	FILTER (?traitTypes != obo:BFO_0000019) .
    	FILTER (?traitTypes != obo:BFO_0000001) .
    	FILTER (?traitTypes != obo:PATO_0002182) .
    	FILTER (?traitTypes != obo:PATO_0000001) .
    	FILTER (?traitTypes != obo:PATO_0001241) .

	# The joins here are largely derived from the relations.csv file
	# since these can be different for different projects we need to think carefully
	# how to address joins in this section for the different projects.
	# Using SPARQL property paths is one possible solution
	?sample rdf:type <http://purl.obolibrary.org/obo/ENVO_00010483> .
	?environmentalTrait obo:BFO_0000050 ?sample.

    	OPTIONAL {?sample urn:observationID ?observationID} . 
    	OPTIONAL {?sample urn:directory ?directory} . 
    	OPTIONAL {?sample urn:source ?source} . 
	OPTIONAL {?sample urn:location_name ?location_name} . 
	OPTIONAL {?sample urn:location_type ?location_type} . 
	OPTIONAL {?sample urn:collected_on ?collected_on} . 
	OPTIONAL {?sample urn:medium ?medium} . 

	OPTIONAL {?environmentalTrait urn:unit ?unit} .
	OPTIONAL {?environmentalTrait urn:variable_name ?variable_name} .
	OPTIONAL {?environmentalTrait urn:variable_desc ?variable_desc} .
	OPTIONAL {?environmentalTrait urn:value ?value} .
}

GROUP BY ?observationID ?directory ?source ?location_name ?location_type ?collected_on ?medium ?unit ?variable_name ?variable_desc ?value
ORDER BY ASC(?observationID)

